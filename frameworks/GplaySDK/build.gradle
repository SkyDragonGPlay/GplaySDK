apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"


    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {

        abortOnError false

    }
}

task jarTask(type: Jar) {
    archiveName = "libgplaysdk-original.jar"
    from('build/intermediates/classes/release')
    from('../GplayRuntimeBridge/build/intermediates/classes/release')
    from('../GplayUnitSDKPlugin/build/intermediates/classes/release')
    destinationDir = file('build/libs')
    exclude('com/skydragon/gplay/BuildConfig.class')
    exclude('com/skydragon/gplay/BuildConfig\$*.class')
    exclude('com/skydragon/gplay/runtime/bridge/BuildConfig.class')
    exclude('com/skydragon/gplay/runtime/bridge/BuildConfig\$*.class')
    exclude('com/skydragon/gplay/unitsdk/plugin/BuildConfig.class')
    exclude('com/skydragon/gplay/unitsdk/plugin/BuildConfig\$*.class')
    exclude('**/R.class')
    exclude('**/R\$*.class')
    include('**/*.class')
}

def androidSDKDir = plugins.getPlugin('com.android.library').sdkHandler.getSdkFolder()
def androidJarDir = androidSDKDir.toString() + '/platforms/' + "${android.compileSdkVersion}" + '/android.jar'

task proguardJar(type: proguard.gradle.ProGuardTask, dependsOn: ['jarTask']) {
    injars('build/libs/libgplaysdk-original.jar')
    outjars('build/libs/libgplaysdk-1.1.0.jar')
    libraryjars(androidJarDir)
    configuration 'proguard-rules.pro'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':frameworks:GplayRuntimeBridge')
    compile project(':frameworks:GplayUnitSDKPlugin')
}